#!/bin/csh 

setenv RSCRIPT /usr/bin/Rscript
setenv NDDIR /home/NucleosomeDynamics
setenv BINDIR $NDDIR/NuclDyn/bin
setenv STATBINDIR $NDDIR/NuclDyn/statistics
setenv HELPDIR $NDDIR/help

setenv DATADIR /data_dir
setenv PUBLICDIR /public_dir
#
if (-e $DATADIR) then
   cd $DATADIR
else
   echo "Warning: $DATADIR does not exist"
endif
if (-e $PUBLICDIR) then
else
   echo "Warning: $PUBLICDIR does not exist"
endif

if ($#argv<1) then
   /bin/cat $HELPDIR/main.hlp
   exit 0
endif

set op=$1
set argv[1]=''

if ($# < 2) then
  set argv=(--help --help)
endif

if ($argv[2] == '--help')  then
  /bin/cat $HELPDIR/header.txt $HELPDIR/$op.hlp
  exit 0
endif

/bin/cat $HELPDIR/header.txt

switch ($op) 
case "run":
   echo "===> Running $op <==="
   echo ""
   /bin/sh $DATADIR/scripts/$2 
   breaksw
case "readBAM":
case "nucleR":
case "NFR": 
case "txstart":
case "periodicity":
case "gaussfitting":
case "nucDyn": 
case "nucleR2structure":
case "toBigWig":
case "fromBigWig":
case "coverage":
case "js_plot":
   echo "===> Running $op <==="
   echo ""
   $RSCRIPT $BINDIR/$op.R $argv
   breaksw
case "nucleR_stats":
case "NFR_stats": 
case "txstart_stats":
case "periodicity_stats":
case "gaussfitting_stats":
case "nucDyn_stats": 
   echo "Running $op"
   $RSCRIPT $STATBINDIR/$op.R $argv
   breaksw
default:
   /bin/cat $HELPDIR/main.hlp

endsw
